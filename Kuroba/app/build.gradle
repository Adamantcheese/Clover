apply plugin: 'com.android.application'

android {
    compileSdkVersion 28

    defaultConfig {
        minSdkVersion 22
        targetSdkVersion 28

        // of the format XXYYZZ, where XX is major, YY is minor, ZZ is patch
        // (watch out for octal notation, never start with a 0)
        // version name should be of the form vXX.YY.ZZ

        //bump the MAJOR (XX) version when there are incompatible changes between versions, or major changes
        //bump the MINOR (YY) version when there are compatible changes between versions (ie database changes)
        //bump the PATCH (ZZ) version otherwise
        versionCode 40100
        versionName "v4.1.0"

        applicationId "com.github.adamantcheese.chan"
        //this is your update endpoint, change it to your repository
        buildConfigField "String", "UPDATE_API_ENDPOINT", "\"https://api.github.com/repos/Adamantcheese/Kuroba/releases/latest\""
        buildConfigField "String", "ISSUES_ENDPOINT", "\"https://github.com/Adamantcheese/Kuroba/issues\""
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    lintOptions {
        abortOnError false
    }

    // Needed for volley
    useLibrary 'org.apache.http.legacy'

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            //this is required for the auto-updater, set the APK to upload to github here
            //if you change this, also change UpdateManager::doUpdate::onSuccess to have the same name
            //see docs/release.txt for more info
            outputFileName = "Kuroba.apk"
        }
    }

    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/LICENSE-LGPL-3.txt'
        exclude 'META-INF/LICENSE-LGPL-2.1.txt'
        exclude 'META-INF/LICENSE-W3C-TEST'
    }

    buildTypes {
        release {
            /*
            If you want to sign releases, make a file in app/keys.properties with the following content:
            keystoreFile=yourkey.store
            keystorePass=pass
            keyAlias=alias
            keyPass=pass
            */
            // This code exists specifically so you don't have to use the "Generate Signed APK" function in Android Studio
            // Just select your build variant (debug/release) and do build -> APK
            File f = file('keys.properties')
            if (f.exists()) {
                Properties props = new Properties()
                InputStream is = new FileInputStream(f)
                props.load(is)

                signingConfigs {
                    release {
                        storeFile file(props['keystoreFile'])
                        storePassword props['keystorePass']
                        keyAlias props['keyAlias']
                        keyPassword props['keyPass']
                    }
                }

                is.close()
                signingConfig signingConfigs.release
            }
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard.cfg'
            debuggable = false
        }
        debug {
            minifyEnabled false
            debuggable = true
        }
    }
}

dependencies {
    implementation 'androidx.legacy:legacy-support-v13:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.recyclerview:recyclerview:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.annotation:annotation:1.0.2'
    implementation 'androidx.exifinterface:exifinterface:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.browser:browser:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'

    implementation 'com.google.android.exoplayer:exoplayer:2.10.1'

    implementation 'com.squareup.okhttp3:okhttp:3.14.1'
    implementation 'com.j256.ormlite:ormlite-core:5.1'
    implementation 'com.j256.ormlite:ormlite-android:5.1'
    implementation 'org.jsoup:jsoup:1.11.3'
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.12'
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view:3.10.0'
    implementation 'org.greenrobot:eventbus:3.1.1'
    implementation 'org.nibor.autolink:autolink:0.10.0'
    implementation 'com.google.code.gson:gson:2.8.5'
    implementation 'me.xdrop:fuzzywuzzy:1.1.10'
    implementation 'org.codejargon.feather:feather:1.0'
    implementation 'com.vladsch.flexmark:flexmark:0.42.12'
    implementation 'com.vladsch.flexmark:flexmark-ext-gfm-issues:0.42.12'
    implementation 'com.vdurmont:emoji-java:4.0.0'
}
